//
// build.gradle
//
// Copyright (c) 2017, 2018, 2019 Couchbase, Inc. All rights reserved.
//
// Licensed under the Couchbase License Agreement (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at:
//     https://info.couchbase.com/rs/302-GJY-034/images/2017-10-30_License_Agreement.pdf
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// Please try to keep this build file as similar to the other family build files
// as is possible.
//
import java.time.Instant
import java.util.regex.Pattern


// ----------------------------------------------------------------
// Plugins
// ----------------------------------------------------------------

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id("org.jetbrains.dokka") version "1.5.0"
}


// ----------------------------------------------------------------
// Constants
// ----------------------------------------------------------------

ext {
    CBL_GROUP = 'com.couchbase.lite'
    CBL_ARTIFACT_ID = 'couchbase-lite-android-ktx'
    CBL_DESCRIPTION = 'Couchbase Lite is an embedded lightweight, document-oriented (NoSQL), syncable database engine.'
    CBL_LICENSE = 'Couchbase, Inc. Community Edition License Agreement'
    CBL_LICENSE_URL = "https://raw.githubusercontent.com/couchbase/product-texts/${getLicenseVersion()}/mobile/couchbase-lite/license/LICENSE_community.txt"
    CBL_PROJECT_URL = 'https://github.com:couchbase/couchbase-lite-java-ce-root.git'
    CBL_ISSUES_URL = 'https://github.com/couchbase/couchbase-lite-java-ce-root/issues'
    CBL_SITE_URL = 'https://developer.couchbase.com/mobile/'

    BUILD_TIME = Instant.now().toString()
    BUILD_RELEASE = file("${ROOT_DIR}/version.txt").text.trim()
    BUILD_NUMBER = (project.hasProperty("buildNumber") && buildNumber) ? buildNumber : "SNAPSHOT"
    BUILD_VERSION = "${BUILD_RELEASE}-${BUILD_NUMBER}"
    BUILD_COMMIT = getBuildId()

    CBL_CORE_DIR = "${ROOT_DIR}/core"
    CBL_COMMON_ROOT_DIR = "${ROOT_DIR}/common"
    CBL_COMMON_DIR = "${CBL_COMMON_ROOT_DIR}/common"
    CBL_COMMON_ANDROID_DIR = "${CBL_COMMON_ROOT_DIR}/android"
    CBL_COMMON_ANDROID_KTX_DIR = "${CBL_COMMON_ROOT_DIR}/android-ktx"
    CBL_CE_ROOT_DIR = "${ROOT_DIR}/ce"
    CBL_CE_COMMON_DIR = "${CBL_CE_ROOT_DIR}/common"
    CBL_CE_ANDROID_DIR = "${CBL_CE_ROOT_DIR}/android"

    CBL_ANDROID_LIB = 'couchbase-lite-android'
    SUPPORT_VERSION = '28.0.0'
}

// Target repo for maven publish
def MAVEN_URL = (project.hasProperty("mavenUrl")) ? mavenUrl : "http://nowhere.void/"

// local.properties
Properties PROPERTIES = new Properties()
PROPERTIES.load(project.rootProject.file("${PROPS_FILE}").newDataInputStream())


// ----------------------------------------------------------------
// Build
// ----------------------------------------------------------------

group = CBL_GROUP
version = BUILD_VERSION

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    // mumbo-jumbo to prevent "More than one file was found" message
    packagingOptions { exclude 'META-INF/library_release.kotlin_module' }

    defaultConfig {
        minSdkVersion ANDROID_MIN_SDK
        targetSdkVersion ANDROID_TARGET_SDK
        versionCode 30000
        versionName BUILD_VERSION
        project.archivesBaseName = "${CBL_ARTIFACT_ID}-${versionName}"

        buildConfigField "String", "VERSION_NAME", "\"${BUILD_VERSION}\""
        buildConfigField "String", "BUILD_TIME", "\"${BUILD_TIME}\""
        buildConfigField "String", "BUILD_COMMIT", "\"${BUILD_COMMIT}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "boolean", "ENTERPRISE", "false"
            buildConfigField "boolean", "CBL_DEBUG", "true"
        }

        release {
            debuggable false
            minifyEnabled false
            buildConfigField "boolean", "ENTERPRISE", "false"
            buildConfigField "boolean", "CBL_DEBUG", "false"
        }
    }

    sourceSets {
        main {
            manifest.srcFile "${CBL_COMMON_ANDROID_KTX_DIR}/main/AndroidManifest.xml"
            java.srcDirs = [
                "${CBL_COMMON_DIR}/main/kotlin",
                "${CBL_COMMON_ANDROID_KTX_DIR}/main/kotlin",
                "${CBL_CE_COMMON_DIR}/main/kotlin",
                "${projectDir}/src/main/kotlin",
            ]
            res.srcDirs = [
                "${CBL_COMMON_ANDROID_KTX_DIR}/main/res",
            ]
        }
        debug {
            manifest.srcFile "${CBL_COMMON_ANDROID_KTX_DIR}/debug/AndroidManifest.xml"
        }
        androidTest {
            manifest.srcFile "${CBL_COMMON_ANDROID_KTX_DIR}/androidTest/AndroidManifest.xml"
            java.srcDirs = [
                "${CBL_COMMON_DIR}/test/java",
                "${CBL_COMMON_ANDROID_DIR}/androidTest/java",
                "${CBL_COMMON_DIR}/test/kotlin",
                "${CBL_CE_COMMON_DIR}/test/java",
                "${CBL_CE_COMMON_DIR}/test/kotlin",
                "${CBL_CE_ANDROID_DIR}/lib/src/androidTest/java",
                "${CBL_COMMON_ANDROID_KTX_DIR}/androidTest/kotlin",
                "${projectDir}/src/androidTest/kotlin",
            ]
            assets.srcDirs = [
                "${CBL_CORE_DIR}/C/tests/data",
                "${CBL_COMMON_DIR}/test/assets",
            ]
            res.srcDirs = [
                "${CBL_COMMON_ANDROID_KTX_DIR}/androidTest/res",
            ]
        }
    }
}

repositories {
    maven { url "http://proget.build.couchbase.com/maven2/cimaven/" }
    google()
    mavenCentral()
}

dependencies {
    compileOnly "com.android.support:support-annotations:${SUPPORT_VERSION}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"

    implementation "com.couchbase.lite:${CBL_ANDROID_LIB}:${BUILD_VERSION}"

    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

/////// Static Analysis

/////// Coverage Report

/////// Documentation


// ----------------------------------------------------------------
// Publication
// ----------------------------------------------------------------

/////// Maven Publishing
// https://docs.gradle.org/current/userguide/publishing_maven.html

def mavenRepoUser = PROPERTIES.getProperty('maven.repo.user')
def mavenRepoPass = PROPERTIES.getProperty('maven.repo.pass')

// Annoying workaround for Android POM generation
def pomConfig = {
    name "${CBL_GROUP}:${CBL_ARTIFACT_ID}"
    description CBL_DESCRIPTION
    url CBL_SITE_URL
    licenses {
        license {
            name CBL_LICENSE
            url CBL_LICENSE_URL
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Couchbase Mobile"
            email "mobile@couchbase.com"
            organization 'Couchbase'
            organizationUrl 'https://www.couchbase.com'
        }
    }
    issueManagement {
        system 'github'
        url CBL_ISSUES_URL
    }
    scm {
        url CBL_SITE_URL
        connection CBL_PROJECT_URL
        developerConnection CBL_PROJECT_URL
    }
}

// Generate dokka.jar
task dokkaJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveBaseName = "${CBL_ARTIFACT_ID}"
    archiveClassifier = 'javadoc'
    from "${buildDir}/dokka/javadoc"
}

// Generate source.jar
task sourcesJar(type: Jar) {
    setArchiveBaseName("${CBL_ARTIFACT_ID}")
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives dokkaJar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            url MAVEN_URL
            credentials {
                username mavenRepoUser
                password mavenRepoPass
            }
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {

            // Note that the these two publications deploy to
            // identical maven coordinates: one will overwrite the other

            libRelease(MavenPublication) {
                groupId CBL_GROUP
                artifactId CBL_ARTIFACT_ID
                version BUILD_VERSION

                artifact bundleReleaseAar
                artifact dokkaJar
                artifact sourcesJar

                // include dependencies
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', CBL_DESCRIPTION)
                    root.children().last() + pomConfig

                    def dependenciesNode = root.appendNode('dependencies')
                    def dep = dependenciesNode.appendNode('dependency')
                    dep.appendNode('groupId', CBL_GROUP)
                    dep.appendNode('artifactId', CBL_ANDROID_LIB)
                    dep.appendNode('version', BUILD_VERSION)
                }
            }

            libDebug(MavenPublication) {
                groupId CBL_GROUP
                artifactId CBL_ARTIFACT_ID
                version BUILD_VERSION

                artifact bundleDebugAar
                artifact dokkaJar
                artifact sourcesJar

                // include dependencies
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', CBL_DESCRIPTION)
                    root.children().last() + pomConfig

                    def dependenciesNode = root.appendNode('dependencies')
                    def dep = dependenciesNode.appendNode('dependency')
                    dep.appendNode('groupId', CBL_GROUP)
                    dep.appendNode('artifactId', CBL_ANDROID_LIB)
                    dep.appendNode('version', BUILD_VERSION)
                }
            }
        }
    }
}


// ----------------------------------------------------------------
// Tasks
// ----------------------------------------------------------------

///////// Dev tasks
task smokeTest(dependsOn: ['compileDebugSources', 'lint'])
task devTest(dependsOn: ['connectedDebugAndroidTest'])
task fullTest(dependsOn: ['smokeTest', 'devTest'])

///////// CI tasks
task ciBuild(dependsOn: ['assembleRelease'])
task ciPublish(dependsOn: ['generatePomFileForLibReleasePublication', 'publishLibReleasePublicationToMavenRepository'])

// ordering (roughly last to first)
project.afterEvaluate {
    connectedDebugAndroidTest.shouldRunAfter smokeTest
    testDebugUnitTest.shouldRunAfter smokeTest
}


// ----------------------------------------------------------------
// Extensions
// ----------------------------------------------------------------

def getLicenseVersion() { return getCommit("product-texts", "master") }

def getBuildId() {
    def hostname = "rogue"
    try { hostname = "hostname".execute().text.trim() }
    catch (Exception ignore) {}

    return "${getCommit("couchbase-lite-java-ce-root", "unofficial").substring(0, 10)}@${hostname}"
}

def getCommit(project, dflt) {
    def manifest = null
    try {
        manifest = new FileReader("${ROOT_DIR}/../manifest.xml")

        def revEx = Pattern.compile("<project.* name=\"${project}\".* revision=\"([\\dabcdef]{40})\"")

        def l
        while ((l = manifest.readLine()) != null) {
            def m = revEx.matcher(l)
            if (m.find()) { return m.group(1) }
        }
    }
    catch (Exception ignore) {}
    finally {
        if (manifest != null) {
            try { manifest.close() }
            catch (Exception ignore) {}
        }
    }

    return dflt
}
