//
// build.gradle
//
// Copyright (c) 2017, 2018, 2019 Couchbase, Inc. All rights reserved.
//
// Licensed under the Couchbase License Agreement (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at:
//     https://info.couchbase.com/rs/302-GJY-034/images/2017-10-30_License_Agreement.pdf
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// Please try to keep this build file as similar to the other family build files
// as is possible.
//


// ----------------------------------------------------------------
// Plugins
// ----------------------------------------------------------------

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}


// ----------------------------------------------------------------
// Constants
// ----------------------------------------------------------------

ext {
    CBL_GROUP = 'com.couchbase.lite'

    BUILD_RELEASE = file("${ROOT_DIR}/version.txt").text.trim()
    BUILD_NUMBER = (project.hasProperty("buildNumber") && buildNumber) ? buildNumber : "SNAPSHOT"
    BUILD_VERSION = "${BUILD_RELEASE}-${BUILD_NUMBER}"

    CBL_CORE_DIR = "${ROOT_DIR}/core"
    CBL_EE_ROOT_DIR = "${ROOT_DIR}/ee"
    CBL_COMMON_ROOT_DIR = "${ROOT_DIR}/common"
    CBL_COMMON_DIR = "${CBL_COMMON_ROOT_DIR}/common"
    CBL_EE_COMMON_DIR = "${CBL_EE_ROOT_DIR}/common"
    CBL_COMMON_ANDROID_DIR = "${CBL_COMMON_ROOT_DIR}/android"

    REPORTS_DIR = "${buildDir}/reports"
    ETC_DIR = "${CBL_COMMON_ROOT_DIR}/etc"
}

// comma separated list of annotations for tests that should not be run.
def TEST_FILTER = (!project.hasProperty("testFilter")) ? null : testFilter


// ----------------------------------------------------------------
// Build
// ----------------------------------------------------------------

group = CBL_GROUP
version = BUILD_VERSION

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion ANDROID_MIN_SDK
        targetSdkVersion ANDROID_TARGET_SDK
        versionCode 30000
        versionName BUILD_VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        if (TEST_FILTER != null) {
            testInstrumentationRunnerArguments notAnnotation: TEST_FILTER
        }
    }

    sourceSets {
        main {
            manifest.srcFile "${CBL_COMMON_ANDROID_DIR}/androidTest/AndroidManifest.xml"
            assets.srcDirs = [
                "${CBL_CORE_DIR}/C/tests/data",                  // lite-core test assets
                "${CBL_COMMON_DIR}/test/assets",                 // Common assets
            ]
        }
        androidTest {
            manifest.srcFile "${CBL_COMMON_ANDROID_DIR}/debug/AndroidManifest.xml"
            java.srcDirs = [
                "${CBL_COMMON_DIR}/test/java",                   // Common tests
                "${CBL_COMMON_ANDROID_DIR}/androidTest/java",    // Common Android tests
                "${CBL_EE_COMMON_DIR}/test/java",                // EE Common tests
                "../lib/src/androidTest/java",                   // EE Android tests
            ]
            assets.srcDirs = [
                "${CBL_CORE_DIR}/C/tests/data",                  // lite-core test assets
                "${CBL_COMMON_DIR}/test/assets",                 // Common assets
            ]
            res.srcDirs = [
                "${CBL_COMMON_ANDROID_DIR}/androidTest/res",     // Common test resources
            ]
        }
    }
}

repositories {
    mavenLocal()
    maven { url "http://proget.build.couchbase.com/maven2/cimaven/" }
    google()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${KOTLIN_VERSION}"

    implementation "com.couchbase.lite:couchbase-lite-android-ee:${BUILD_VERSION}"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${KOTLIN_VERSION}"
    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${KOTLIN_VERSION}"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


// ----------------------------------------------------------------
// Tasks
// ----------------------------------------------------------------

// This target requires setting the property "automatedTests" true.
// See settings.gradle for explanation.
task ciTest(dependsOn: ['connectedDebugAndroidTest'])
